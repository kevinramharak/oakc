;; NOTE: the compiler will hoist __CORE_GLOBAL_SCOPE_SIZE and __CORE_INIT_VM_CAPACITY as a prefix to the contents of this file

;; we want the runtime to be be able to host 'itself' by not depending on any dependencies
;; it should only implement the vm and any nessesary code and variables to run the oak program
;; the only overhead it currently contains is having a function pointer table for initializer functions
;; core only uses 1 entry, the rest of the entries can be filled during a 'linking' process to hook into this initializer feature

;; Setting the binary offset to 0 makes sure our .data section starts at address 0x0000
org 0

;; data segment -
;; NOTE: the _0x00xx labels are just to help visualize the offsets
;; NOTE: there are some static variables scattered trough the source, decide on what stays static inline and what can move to the data section
.data
_0x0000:
    __core_interrupt_vector_table:
    ;; division by zero causes an interrupt
    __core_interrupt_divide_by_zero: dw __core_interrupt_handler_default
_0x0001:
    ;; not implemented
    __core_interrupt_trap: dw __core_interrupt_handler_default
_0x0002:
    ;; not implemented
    __core_interrupt_nmi: dw __core_interrupt_handler_default
_0x0003:
    ;; NOTE: INT 3 is a special case for the server that starts the debugging mode.
    ;; NOTE: This memory location will never actually be used as a jump location for an interrupt
    ;; TODO: use this location for a static variable so we dont waste its space
    ;; not implemented server
    __core_interrupt_break_point: dw __core_interrupt_handler_default
_0x0004:
    ;; this interrupt requires a INTO instruction and the overflow flag to be set
    __core_interrupt_integer_overflow: dw __core_interrupt_handler_default
_0x0005:
    ;; keep 0x05 - 0x1F reserved for future interrupt handlers
    dw 0x1B dup(0x0153)
_0x0020:
    ;; MAR server causes this interrupt when the cpu is not allowed to execute any more instructions
    ;; gives 10_0000 instructions to save state and brk.
    ;; If execution is forcibly stopped the behaviour of the runtime is undefined
    __core_interrupt_execution_limit_reached: dw __core_interrupt_handler_execution_limit_reached
_0x0021:
    ;; keep 0x20 - 0x3f reserverd for future (hardware) interrupt handlers
    __core_interrupt_vector_table_hardware: dw 0x001F dup(0x0153)
_0x0040:
    ;; keep 0x40 - 0x43 reserved for core interrupt handlers
    __core_interrupt_vector_table_core:
    ;; software interrupt for a panic of unkown reason
    __core_interrupt_panic_unkown: dw __core_interrupt_handler_default
_0x0041:
    ;; software interrupt for a panic caused by a stack and heap collision
    __core_interrupt_panic_stack_heap_collision: dw __core_interrupt_handler_panic_stack_heap_collision
_0x0042:
    ;; software interrupt for a panic caused by having no more free memory
    __core_interrupt_panic_no_free_memory: dw __core_interrupt_handler_panic_no_free_memory
_0x0043:
    ;; software interrupt for a panic caused by a stack underflow
    __core_interrupt_panic_stack_underflow: dw __core_interrupt_handler_panic_stack_underflow
    ;; 0x44 - 0x7f is open for software interrupts
_0x0044:
    __core_interrupt_vector_table_software:
    dw 0x3C dup(0x0153)
    ;; TODO: figure out how the stdlib is going to reserve interrupts
    ;; probably by owning the software interrupt table and providing an interface to interact with it
_0x0080:
    ;; NOTE: the following line generates a intializer vector table
    ;; %[generate_initializer_table] 0x3c
    dw 0, 0x3b dup(0xdead)
    ;; struct {
    ;;   size_t length
    ;;   void * entries[length]
    ;; } __core_initializer_vector_table;
    ;; NOTE: the following line generates a destructor vector table
    ;; %[generate_destructor_table] 0x3c
    dw 0, 0x3b dup(0xdead)
    ;; struct  {
    ;;   size_t length
    ;;   void * entries[length]
    ;; } __core_destructor_vector_table;
_0x00F8:
    ;; static variables used by __core_* functions
    ;; static struct machine {
    __core_vm:
    ;;   int * memory
    __core_vm_memory: dw 0x0000
    ;;   bool * allocated
    __core_vm_allocated: dw 0x0000
    ;;   size_t capacity
    __core_vm_capacity: dw 0x0000
    ;;   size_t base_ptr
    __core_vm_base_ptr: dw 0x0000
    ;;   size_t stack_ptr
    __core_vm_stack_ptr: dw 0x0000
    ;; } vm;
    ;; static const char * __core_panic_unkown_message
    __core_panic_unkown_message: dw "\nERR_UNKNOWN", 0 ;; 13 length
    ;; static const char * __core_panic_stack_heap_collision
    __core_panic_stack_heap_collision: dw "\nERR_STACK_HEAP_COLLISION", 0 ;; 26 length
    ;; static const char * __core_panic_no_free_memory
    __core_panic_no_free_memory: dw "\nERR_NO_FREE_MEMORY", 0 ;; 20 length
    ;; static const char * __core_panic_stack_underflow
    __core_panic_stack_underflow: dw "\nERR_STACK_UNDERFLOW", 0 ;; 21 length
    ;; static void * __core_jump_on_wake
    __core_jump_on_wake: dw __core_initial_jump_on_wake ;; intial value is points to initialization, which makes the `jmp [__core_jump_on_wake]` a no-op
    ;; static size_t __core_execution_limit_reached_saved_sp
    __core_execution_limit_reached_saved_sp: dw 0
    ;; static size_t __core_execution_limit_reached_saved_bp
    __core_execution_limit_reached_saved_bp: dw 0
_0x0150:
    ;; 3 words reserved for execution cost variables. These are set by the server executing the code
    ;; see https://github.com/simon987/Much-Assembly-Required/blob/master/Server/src/main/java/net/simon987/server/assembly/CPU.java#L194
    __core_execution_cost_addr: dw 0x0000
    ;; 32 bit signed integer
    __core_executed_instruction_count: dw 0x0000, 0x0000

_0x0153:
;; #[interrupt_handler] #[no_return] void __core_interrupt_handler_default()
;; this handler can be used as the default address for interrupt handlers
;; this prevents unhandled interrupts from jumping to random locations
__core_interrupt_handler_default:
    ;; currently the label just falls trough to the panic_unkown handler

;; #[interrupt_handler] void __core_interrupt_handler_panic_unkown()
__core_interrupt_handler_panic_unkown:
    push 0
    call __core_panic
    
__CORE_STACK_HEAP_COLLISION EQU 1
__CORE_NO_FREE_MEMORY EQU 2
__CORE_STACK_UNDERFLOW EQU 3

_0x0157:
;; #[interrupt_handler] #[no_return] void __core_interrupt_handler_panic_stack_heap_collision()
__core_interrupt_handler_panic_stack_heap_collision:
    push __CORE_STACK_HEAP_COLLISION
    call __core_panic
_0x015B:
;; #[interrupt_handler] #[no_return] void __core_interrupt_handler_panic_no_free_memory()
__core_interrupt_handler_panic_no_free_memory:
    push __CORE_STACK_HEAP_COLLISION
    call __core_panic
_0x015F:
;; #[interrupt_handler] #[no_return] void __core_interrupt_handler_panic_stack_underflow()
__core_interrupt_handler_panic_stack_underflow:
    push __CORE_STACK_UNDERFLOW
    call __core_panic
_0x0163:

;; #[interrupt_handler] void __core_interrupt_handler_execution_limit_reached()
;; this has to be in core to make sure a program keeps its execution state in between ticks
;; std can overwrite the interupt vector entry to a different location to add more features (like a __internal_comport_flush call)
__core_interrupt_handler_execution_limit_reached:
    push a
    push b
    push c
    push d
    push x
    push y
    mov [__core_execution_limit_reached_saved_sp], sp
    mov [__core_execution_limit_reached_saved_bp], bp
    mov [__core_jump_on_wake], __core_interrupt_handler_execution_limit_reached_on_wake
    brk
__core_interrupt_handler_execution_limit_reached_on_wake:
    mov bp, [__core_execution_limit_reached_saved_bp]
    mov sp, [__core_execution_limit_reached_saved_sp]
    pop y
    pop x
    pop d
    pop c
    pop b
    pop a
    iret

;; #[no_return] void __core_panic(int error_code)
;; prints a user friendly error message and then exits the program
;; in our case this means spinning forever
;; because it has a no return flag this function does not have to preserve registers
__core_panic:
    ;; [SP] = return address
    ;; [SP + 1] = int error_code
    MOV A, [SP + 1]
    mov X, __core_panic_unkown_message
__core_panic_case_1:
    cmp A, 1
    jnz __core_panic_case_2
    mov X, __core_panic_stack_heap_collision
    jmp __core_panic_end
__core_panic_case_2:
    cmp A, 2
    jnz __core_panic_case_3
    mov X, __core_panic_no_free_memory
    jmp __core_panic_end
__core_panic_case_3:
    cmp A, 3 
    jnz __core_panic_end
    mov X, __core_panic_stack_underflow
__core_panic_end:
    mov A, 3 ;; COMPORT_SELF_OUT
__core_panic_write:
    hwi 0x0D ;; HWID_COMPORT
    xor B, B ;; reset b
__core_panic_scan:
    inc X
    inc B
    cmp [X], 0 ;; this copies a null terminated string to the comport's internal buffer, then spins
    jz __core_spin
    cmp B, 8
    jz __core_panic_write
    jmp __core_panic_scan

;; %[runtime_initializer] __core_machine_new
;; void __core_machine_new()
;; initializes the virtual machine located at the label __core_vm
;; we expected a label to be at the end of all code named `__core_heap_start`
__core_machine_new:
    ;; TODO: write an heap implementation that doesnt have a 100% memory overhead
    ;; compiler figures out how big the tape must be and defines it as `__core_init_vm_capacity: dw value`
    mov [__core_vm_capacity], __CORE_INIT_VM_CAPACITY
    ;; vm->memory is a pointer to the __core_heap_start address
    mov [__core_vm_memory], __core_heap_start

    ;; vm->allocated  is a pointer to the __core_heap_start address with an offset of vm->capacity
    mov [__core_vm_allocated], __core_heap_start
    add [__core_vm_allocated], [__core_vm_capacity]
    ;; compiler figures out how big the stack can be and defines it as `__core_global_scope_size: dw value`
    ;; the c impl does a for loop with push(0) for this size, but the end result is the same as setting the stack ptr to this size
    mov [__core_vm_stack_ptr], __CORE_GLOBAL_SCOPE_SIZE
    ret

;; void __core_machine_load_base_ptr()
;; push the base pointer onto the stack
__core_machine_load_base_ptr:
    push [__core_vm_base_ptr]
    call __core_machine_push
    ret

;; void __core_machine_establish_stack_frame(size_t arg_size, size_t local_scope_size)
__core_machine_establish_stack_frame:
    push x
    push BP
    mov BP, SP
    
    ;; [BP] == BP
    ;; [BP + 1] == x
    ;; [BP + 2] == return address
    ;; [BP + 3] == size_t arg_size
    ;; [BP + 4] == size_t local_scope_size

    ; int args[arg_size]
    ; int * ptr = args[args.length - 1]

    ;; Pop the arguments' values off of the vm stack onto the mar stack
    ; while (ptr != args) {
    ;   *ptr = machine_pop();
    ;    ptr -= 1;
    ; }
    mov x, [BP + 3]
__core_machine_establish_stack_frame_preserve_args_loop:
    cmp x, 0
    jz __core_machine_establish_stack_frame_preserve_args_break
    call __core_machine_pop
    push a
    dec x
    jmp __core_machine_establish_stack_frame_preserve_args_loop
__core_machine_establish_stack_frame_preserve_args_break:

    ;; Push the current base pointer onto the stack so that
    ;; when this function returns, it will be able to resume
    ;; the current stack frame
    call __core_machine_load_base_ptr

    ;; Set the base pointer to the current stack pointer to 
    ;; begin the stack frame at the current position on the stack.
    mov [__core_vm_base_ptr], [__core_vm_stack_ptr]

    ;; allocate space for local variables
    ;; NOTE: the vm impl in C actually pushes 0's
    add [__core_vm_stack_ptr], [BP + 4]

    ;; Push the arguments back onto the vm stack from the mar stack
__core_machine_establish_stack_frame_restore_args_loop:
    cmp [BP + 3], 0
    jz __core_machine_establish_stack_frame_restore_args_return
    call __core_machine_push
    dec [BP + 3]
    jmp __core_machine_establish_stack_frame_restore_args_loop

__core_machine_establish_stack_frame_restore_args_return:
    mov SP, BP
    pop BP
    pop x
    ret 2

;; void __core_machine_end_stack_frame(size_t return_size, size_t local_scope_size);
__core_machine_end_stack_frame:
    push x
    push BP
    mov BP, SP
    
    ;; [BP] == BP
    ;; [BP + 1] == x
    ;; [BP + 2] == return address
    ;; [BP + 3] == size_t return_size
    ;; [BP + 4] == size_t local_scope_size

    ; int return_value[return_size]
    ; int * ptr = return_value[return_value.length - 1]

    ;; Pop the returned values off of the stack
    ; while (ptr != return_value) {
    ;   *ptr = machine_pop();
    ;    ptr -= 1;
    ; }
    mov x, [BP + 3]
__core_machine_end_stack_frame_preserve_return_value_loop:
    cmp x, 0
    jz __core_machine_end_stack_frame_preserve_return_value_clear_stack
    call __core_machine_pop
    push a
    dec x
    jmp __core_machine_end_stack_frame_preserve_return_value_loop

__core_machine_end_stack_frame_preserve_return_value_clear_stack:
    ;; Discard the memory setup by the stack frame
    ;; NOTE: the c impl actually pops these (and pop 0's out the memory)
    sub [__core_vm_stack_ptr], [BP + 4]

    ;; Retrieve the parent function's base pointer to resume the function
    call __core_machine_pop
    mov [__core_vm_base_ptr], A
    
    ;; Finally, push the returned value back onto the stack for use by
    ;; the parent function.
__core_machine_end_stack_frame_restore_return_value_loop:
    cmp [BP + 3], 0
    jz __core_machine_end_stack_frame_restore_return_value_return
    call __core_machine_push
    DEC [BP + 3]
    jmp __core_machine_end_stack_frame_restore_return_value_loop

__core_machine_end_stack_frame_restore_return_value_return:
    mov SP, BP
    pop BP
    pop x
    ret 2

;; void __core_machine_allocate()
;; pop size of the stack
;; TODO: allocate variables on the stack so we can save a few push/pop instructions
__core_machine_allocate:
    push B
    push C
    push D
    push X
    push Y
    push BP
    mov BP, SP
    call __core_machine_pop      ; int size = __core_machine_pop()
    mov B, [__core_vm_capacity]  ; int i = vm->capacity
    dec B                        ; i -= 1
    mov C, [__core_vm_allocated] ; int * loc_allocated = vm->allocated
    xor X, X                     ; int consecutive_free_cells = 0
    xor Y, Y                     ; addr = 0
__core_machine_allocate_loop_find:
    cmp B, [__core_vm_stack_ptr] ; while (i > vm->stack_ptr)
    jle __core_machine_allocate_loop_break
    mov D, C                     ; 
    add D, B                     ; int is_allocated = vm->allocated[i]
    cmp [D], 0                   ; if is_allocated == 0
    jz __core_machine_allocate_loop_cell_free
    xor x, x
    jmp __core_machine_allocate_loop_check
__core_machine_allocate_loop_cell_free:
    inc x
__core_machine_allocate_loop_check:
    cmp X, A
    jz __core_machine_allocate_loop_break_success
    dec b
    jmp __core_machine_allocate_loop_find
__core_machine_allocate_loop_break_success:
    mov Y, B                   ; int addr = i
__core_machine_allocate_loop_break:
    cmp Y, [__core_vm_stack_ptr] ; if addr <= vm->stack_ptr then panic
    jle __core_machine_allocate_loop_panic
    xor B, B                   ; i = 0
__core_machine_allocate_loop_mark:
    cmp B, A                   ; while (i < size)
    jge __core_machine_allocate_return
    mov D, C                   ; int * loc = vm->allocated
    add D, Y                   ; loc += addr
    add D, B                   ; loc += i
    mov [D], 1                 ; *loc = 1
    inc B
    jmp __core_machine_allocate_loop_mark
__core_machine_allocate_return:
    push Y
    call __core_machine_push
    mov SP, BP
    pop BP
    pop Y
    pop X
    pop D
    pop C
    pop B
    ret
__core_machine_allocate_loop_panic:
    int __core_interrupt_panic_no_free_memory

;; void __core_machine_free()
;; TODO: allocate variables on the stack so we can save a few push/pop instructions
__core_machine_free:
    push B
    push C
    push D
    push X
    push BP
    mov BP, SP
    call __core_machine_pop         ; int addr = __core_machine_pop() // C
    mov C, A
    call __core_machine_pop         ; int size = __core_machine_pop() // A
    xor B, B                 ; int i = 0                // B
__core_machine_free_loop:
    cmp B, A
    jl __core_machine_free_return
    mov D,   C                 ; int d = addr + i
    add D, B
    mov X, [__core_vm_allocated]
    add X, D
    mov [X], 0                 ; vm->allocated[addr + i] = false
    mov X, [__core_vm_memory]
    add X, D
    mov [X], 0                 ; vm->memory[addr + i] = 0
    inc B
    jmp __core_machine_free_loop
__core_machine_free_return:
    mov SP, BP
    pop BP
    pop X
    pop D
    pop C
    pop B
    ret

;; void __core_machine_push(int n)
;; push n to the stack
__core_machine_push:
    push BP
    mov BP, SP
    ;; if vm->allocated[vm->stack_ptr] !== 0 then panic
    mov A, [__core_vm_allocated]
    add A, [__core_vm_stack_ptr]
    cmp [A], 0
    jnz __core_machine_push_panic
    mov A, [__core_vm_memory]
    add A, [__core_vm_stack_ptr] ; ref = &vm->memory[vm->stack_ptr]
    mov [A], [BP + 2]          ; *ref = n
    inc [__core_vm_stack_ptr]    ; vm->stack_ptr++
    mov SP, BP
    pop BP
    ret 1
__core_machine_push_panic:
    int __core_interrupt_panic_stack_heap_collision

;; int __core_machine_pop()
;; pop n of the stack
;; TODO: allocate variables on the stack so we can save a few push/pop instructions
__core_machine_pop:
    push B
    push BP
    mov BP, SP
    cmp [__core_vm_stack_ptr], 0
    jz __core_machine_pop_panic
    mov A, [__core_vm_memory]    ; int * loc = vm->memory
    add A, [__core_vm_stack_ptr] ; loc += vm->stack_ptr
    dec A                      ; loc -= 1
    mov B, [A]                 ; int b = *loc
    mov [A], 0                 ; *loc = 0 // do we really need to 0 it?
    dec [__core_vm_stack_ptr]    ; vm->stack_ptr--
    mov A, B                   ; return values are set in the A register
    mov SP, BP
    pop BP
    pop B
    ret
__core_machine_pop_panic:
    int __core_interrupt_panic_stack_underflow

;; void __core_machine_store(int size)
;; pop address of the stack
;; i is size - 1
;; for i until size
;;    pop value off the stack
;;    write it to address + i
;; TODO: allocate variables on the stack so we can save a few push/pop instructions
__core_machine_store:
    push B
    push C
    push D
    push BP
    mov BP, SP
    call __core_machine_pop        ; int * addr = __core_machine_pop()
    add A, [__core_vm_memory] ; addr += vm->memory
    mov C, A                ; // move addr to a different register
    mov B, [BP + 5]         ; int i = size
    dec B                   ; i -= 1
__core_machine_store_loop:
    cmp B, 0                ;   while (i >= 0)
    jl __core_machine_store_return
    call __core_machine_pop        ;   int value = __core_machine_pop()
    mov D, C                ;   int * loc = addr
    add D, B                ;   loc += i
    mov [D], A              ;   *loc = value
    dec B                   ;   i--
    jmp __core_machine_store_loop
__core_machine_store_return:
    mov SP, BP
    pop BP
    pop D
    pop C
    pop B
    ret 1

;; void __core_machine_load(int size)
;; load size amount of integers from popped address
;; TODO: allocate variables on the stack so we can save a few push/pop instructions
__core_machine_load:
    push B
    push C
    push D
    push BP
    mov BP, SP
    call __core_machine_pop        ; int * addr = __core_machine_pop()
    add A, [__core_vm_memory] ; addr += vm->memory
    mov C, A                ; move addr to a different register
    xor B, B                ; int i = 0
__core_machine_load_loop:
    cmp B, [BP + 5]         ; while (i < size)
    jge __core_machine_load_return
    mov D, C                ; int * loc = addr
    add D, B                ; loc += i
    push [D]                ; int value = *loc
    call __core_machine_push       ; __core_machine_push(value)
    inc B                   ; i++
    jmp __core_machine_load_loop
__core_machine_load_return:
    mov SP, BP
    pop BP
    pop D
    pop C
    pop B
    ret 1

;; void __core_machine_add()
;; pop 2 values, add them and push the result
;; TODO: allocate variables on the stack so we can save a few push/pop instructions
__core_machine_add:
    push B
    push BP
    mov BP, SP
    call __core_machine_pop
    mov B, A
    call __core_machine_pop
    add A, B
    push A
    call __core_machine_push
    mov SP, BP
    pop BP
    pop B
    ret

;; void __core_machine_subtract()
;; pop 2 values, subtract them and push the result
;; TODO: allocate variables on the stack so we can save a few push/pop instructions
__core_machine_subtract:
    push B
    push BP
    mov BP, SP
    call __core_machine_pop
    mov B, A
    call __core_machine_pop
    sub A, B
    push A
    call __core_machine_push
    mov SP, BP
    pop BP
    pop B
    ret

;; void __core_machine_sign()
;; pop a value of the stack, if it is 0 or greater push 1, else push -1
;; TODO: allocate variables on the stack so we can save a few push/pop instructions
__core_machine_sign:
    push B
    push BP
    mov BP, SP
    call __core_machine_pop
    cmp A, 0
    jge __core_machine_sign_ge
__core_machine_sign_l:
    push -1
    jmp __core_machine_sign_push
__core_machine_sign_ge:
    push 1
__core_machine_sign_push:
    call __core_machine_push
    mov SP, BP
    pop BP
    pop B
    ret

;; void __core_machine_multiply()
;; pop 2 values, multiply them and push the result
;; TODO: allocate variables on the stack so we can save a few push/pop instructions
__core_machine_multiply:
    push B
    push BP
    mov BP, SP
    call __core_machine_pop
    mov B, A
    call __core_machine_pop
    mul B                 ; will multiply register A with operand, result is a 32-bit integer in Y:A
    push A
    call __core_machine_push
    mov SP, BP
    pop BP
    pop B
    ret

;; void __core_machine_divide()
;; pop 2 values, divide them and push the result
;; TODO: allocate variables on the stack so we can save a few push/pop instructions
__core_machine_divide:
    push B
    push BP
    mov BP, SP
    xor Y, Y
    call __core_machine_pop
    mov B, A
    call __core_machine_pop
    div B                 ; will divide register Y:A with source, result in A, remainder in Y
    push A
    call __core_machine_push
    mov SP, BP
    pop BP
    pop B
    ret

;; void __core_init()
;; will call all functions registered in the __core_initializer_vector_table
;; the table itself is generated at compile time
;; TODO: make it setup argc, argv and env
__core_init:
    xor a, a                                          ; size_t i = 0
__core_init_loop:
    cmp a, [__core_initializer_vector_table_length]   ; while (i != __core_initializer_vector_table_length)
    jz __core_init_return
    mov b, __core_initializer_vector_table_entries    ; void (*func)() = __core_initializer_vector_table_entries[i]
    add b, a
    call [b]                                            ; (*func)()
    inc a                                             ; i++
    jmp __core_init_loop
__core_init_return:
    ret

;; void __core_exit(int code)
;; will call any registered destructors in the __core_destructor_vector_table
;; the table itself is generated at compile time
__core_exit:
    xor a, a                                          ; size_t i = 0
__core_exit_loop:
    cmp a, [__core_destructor_vector_table_length]   ; while (i != __core_destructor_vector_table_length)
    jz __core_exit_return
    mov b, __core_destructor_vector_table_entries    ; void (*func)() = __core_destructor_vector_table_entries[i]
    add b, a
    call [b]                                            ; (*func)()
    inc a                                             ; i++
    jmp __core_exit_loop
__core_exit_return:
    ret 1

;; #[entry] void __core_start()
;; execution on a server tick always starts at .text with the registers and status flags reset
;; a server reset will also reset the memory and floppy contents
.text
__core_start:
    ;; jump to the location stored in __core_jump_on_wake
    ;; default value will be the next instruction making this a no-op
    jmp [__core_jump_on_wake]
__core_initial_jump_on_wake:
    ;; call runtime init
    call __core_init

    ;; call the user defined enry point
    call __core_main

    ;; call runtime exit, assuming __core_main left a return value in A
    push a
    call __core_exit

    ;; set the __core_jump_on_wake pointer to the next instruction
    ;; by using a simple pointer to represent a jump location if not null
    ;; any implementation can use `__core_jump_on_wake` and `brk` to implement a sleep and wake implementation with more features
__core_spin:
    mov [__core_jump_on_wake], __core_brk
__core_brk:
    brk

;; oak compile will generate the user program (and stdlib if included) below